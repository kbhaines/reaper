desc:VSL SE Articulations
slider1:art_type=0<0,2,1{C1/C2,C1/C7 (Cello),C6/C7}>Articulation KS Config

in_pin:none
out_pin:none

@init
function blip_note(offset, note) (
  midisend(offset, $x90, note, 127);
  midisend(offset, $x80, note, 0);
);
dim1=(art_type < 2 ? 24 : 84);
dim2=(art_type == 0 ? 36 : 96);
  
@block
while (midirecv(offset,msg1,msg2,msg3)) ( // REAPER 4.59+ syntax while()
  (msg1 == $xC0 ) ? (
    (msg2 < 126) ? (
      blip_note(offset, dim1 + (msg2 >> 4));
      d2 = msg2 & 15; 
      d2 > 0 ? blip_note(offset, dim2+d2-1);
    ) : (
      (msg2 == 126 ) ? midisend(offset, $xB0, 28, 127) : midisend(offset, $xB0, 28, 0)
     )
  ) : midisend(offset,msg1,msg2,msg3); // passthrough other events
); 

/**
JSFX Name: Articulation Mapper
Author: happyrat
Version: 1.0.4
Changelog: 
  1.0.3 Initial Release
  1.0.4 Strip 'custom' annotation from the beginning of articulations.
  1.0.5 Add Spitfire SO keyswitches for entire orchestra
Link: GitHub Home https://github.com/kbhaines/reaper/blob/main/articulation-mapper/README.md
Provides: 
  [data] articulation-maps/*.txt
About:
  # Articulation Mapper

  This JSFX plugin allows you to define articulations which are triggered via
  Reaper's built-in notation features. This is similar to Expression Maps in
  Cubase or Articulation Sets in LogicPro.

  See the GitHub home page for full instructions:

  https://github.com/kbhaines/reaper/blob/main/articulation-mapper/README.md

  Features:

  - Articulations are triggered by notation attached to notes
  - Midi Notes and CC triggers, up to 4 per articulation
  - Simultaneous notes can have different articulations (no need to slightly separate the notes)
  - Up to 80 articulations per instance
  - UI displays currently triggered articulation
  - Default articulation can be set (when notes have no notation attached)
  - Default articulation can be changed by keyswitch (allows you to mix keyswitching & notation)
  - Simple text file based configuration (examples for Spitfire & VSL included)

**/

desc:Articulation Mapper
in_pin:none
out_pin:none

slider1:/articulation-maps:vsl-special-vi14.txt:Articulation Map
slider2:use_default=0<0,80,1>Default Articulation # (0=none)
slider3:aux_keyswitch=0<0,10,1{Off, C-1,C0,C1,C2,C3,C4,C5,C6,C7,C8>Change Default by keyswitch

@init
ext_midi_bus = 1.0;

MAX_ARTICULATIONS = 80;
NUM_ARTS = 0;
ACTIVE_ART = -1;

// Store in global string array
art_match_strings = 1;
art_keys_str = art_match_strings + MAX_ARTICULATIONS;

// Type is 4 8-bit values; each 8 bits is either 1-127 for normal notes (note + 1), 
// or 128-255 for a midi CC id (CCid + 128). The lowest 8 bits are the first
// part of the switch sequence to send
art_ks_type = 1;

// KS midi are 4 8-bit values, which depending on the corresponding 8bits in
// art_ks_type can be either note velocity or CC data.
art_ks_data = art_ks_type + MAX_ARTICULATIONS;

// note_art is an array holding a midi note number's assigned articulation
// indexed by chan * 128 + note_id
note_art = art_ks_data + MAX_ARTICULATIONS;
note_art_size = 128 * 16;
memset(note_art, -1, note_art_size);

function parse_note_seq(note_seq, idx)
  local(seq, note, oct, acc, i, rest, type, data) (
  data_32 = 0;
  type_32 = 0;
  i = 0;
  type = 0;
  data = 0;
  strcpy(seq, note_seq);
  while (strlen(seq) > 1 && i < 4 && type != -1) (
    type = -1;
    data = 127;
    match("cc%d.%d%0s", seq, cc_id, cc_data, #rest) ? (type = 128 + cc_id; data=cc_data):
    match("C%d.%d%0s", seq, oct, vel, #rest) ? (type = 0; data = vel):
    match("C%d%0s", seq, oct, #rest)  ? ( type = 0 ):
    match("C#%d%0s", seq, oct, #rest) ? ( type = 1 ):
  
    match("D%d%0s", seq, oct, #rest)  ? ( type = 2 ):
    match("D#%d%0s", seq, oct, #rest) ? ( type = 3 ):
  
    match("E%d%0s", seq, oct, #rest)  ? ( type = 4 ):
    match("F%d%0s", seq, oct, #rest)  ? ( type = 5 ):
    match("F#%d%0s", seq, oct, #rest) ? ( type = 6 ):
  
    match("G%d%0s", seq, oct, #rest)  ? ( type = 7 ):
    match("G#%d%0s", seq, oct, #rest) ? ( type = 8 ):
    match("A%d%0s", seq, oct, #rest)  ? ( type = 9 ):
    match("A#%d%0s", seq, oct, #rest) ? ( type = 10 ):
    match("B%d%0s", seq, oct, #rest)  ? ( type = 11 ):(type = -1); // TODO!

    type != -1 ? (
      // If a note, add octave offset
      type < 128 ? type += 1 + (oct + 1)*12;

      strcpy(seq, #rest);
      // type 0-127 = note ID, data = velocity
      // type 128-255 = ccID, data = cc_value 
      //type += cc_id  << (i * 8);
      type_32 += type << (i * 8);
      data_32 += data << (i * 8);
      i+=1;
    );
  );
  type > -1 ? (
    art_ks_type[idx] = type_32;
    art_ks_data[idx] = data_32;
  );
  type;
);

function load_arts() local (handle, i, art_str, keys, art_name) (

  strcpy_fromslider(#art_filename, slider1);
  !((handle = file_open(#art_filename)) < 0) ? ( // file_open successful
    i = 0;
    while (file_string(handle,#art_str) > 0 && i < MAX_ARTICULATIONS) (
      !match(";*", #art_str) ? (
        match("%S %S;*\n*",#art_str,#keys,#art_name) ||
        match("%S %s*\n*",#art_str,#keys,#art_name) ||
        match("%S %S;*\r*",#art_str,#keys,#art_name) ||
        match("%S %s*\r*",#art_str,#keys,#art_name)
        ? (
          strcpy(art_match_strings+i, #art_name);
          strcpy(art_keys_str+i, #keys);
          parse_note_seq(#keys, i) != -1 ? i += 1;
        );
      );
    );
    file_close(handle);
    NUM_ARTS = i;
  );
);

function parse_notation(note_art_str) (
  art_match = -1;
  scan = 0;
  while (scan < NUM_ARTS && (art_match < 0)) (
    match(art_match_strings + scan, note_art_str) ? 
      art_match = scan;
    scan += 1;
  );
  art_match;
);

function is_text_evt(msg1, msg2) (
  msg1 == $xFF && msg2 == $x0F;
);


function is_note_on(msg1) (
  (msg1 & $xF0) == $x90;
);

function send_ks_for_articulation(offset, chan, art_id) local (ks_type, ks_data, type, data) (
  art_id != ACTIVE_ART ? (
    ks_type = art_ks_type[art_id];
    ks_data = art_ks_data[art_id];
    
    // Make two passes; 1st pass is note-on and midi CC,
    // 2nd pass is note-off only. This allows UACC/KS to combine
    // articulations.
    while (ks_type) (
      type = ks_type & $xFF;
      data = ks_data & $xFF;
      type > 127 ? 
        midisend(offset, $xB0 + chan, type-128, data): 
        midisend(offset, $x90+chan, type-1, data);
      ks_type = ks_type >> 8;
      ks_data = ks_data >> 8;
    );
    ks_type = art_ks_type[art_id];
    ks_data = art_ks_data[art_id];
    while (ks_type) (
      type = ks_type & $xFF;
      data = ks_data & $xFF;
      type < 128 ? midisend(offset, $x80+chan, type-1, 0);
      ks_type = ks_type >> 8;
      ks_data = ks_data >> 8;
    );
    ACTIVE_ART = art_id;
  );
);

function set_articulation(str) (
  strcpy_from(#msga, #str, 2);
  match("NOTE %d %d custom %s", #msga, note_chan, note_id, #art_str) ||
  match("NOTE %d %d %s", #msga, note_chan, note_id, #art_str) ? (
    note_art[note_chan * 128 + note_id] = parse_notation(#art_str);
  );
);

function check_articulation(offset, chan, note_id) (
  i = chan * 128 + note_id;
  art_id = note_art[i];
  art_id == -1 && use_default? art_id = use_default-1;
  art_id >= 0 ? (
    send_ks_for_articulation(offset, chan, art_id);
    note_art[i] = -1;
  );
);
  
  
function handle_aux_keyswitch(note) local (res) (
  aux_keyswitch > 0 ? (
    lower = (aux_keyswitch-1) * 12;
    note >= lower && note < lower + 12 ? (
      use_default = 1 + note - lower;
    );
  );
);

@slider
load_arts();

@block
while (midirecv_str(offset, #str)) (
  msg1 = str_getchar(#str, 0);
  msg2 = str_getchar(#str, 1);
  msg3 = str_getchar(#str, 2);
  
  is_text_evt(msg1, msg2) ? (
    set_articulation(str);
  ) : is_note_on(msg1) ? (
    handle_aux_keyswitch(msg2);
    check_articulation(offset, msg1 & $x0F, msg2);
  );
  midisend_str(offset, #str);
);


@gfx 360 360
margin_left = 10;
margin_top = 10;
text_colour = 0.9;
drawn_art = -2;
NUM_ARTS > 0 && drawn_art != ACTIVE_ART ? ( 
  gfx_setfont(1, "arial", 12, 'b');
  gfx_measurestr("00. G#9G#9G#9G9", note_width, note_height);
  gfx_a = 1;
  gfx_r = gfx_g = gfx_b = text_colour;
  gfx_x = margin_left;
  gfx_y = margin_top;
  gfx_printf("%d articulations loaded\n", NUM_ARTS);
  i = 0;
  split_at = floor((NUM_ARTS+1) / 2);
  loop(NUM_ARTS,
    gfx_x = left = i < split_at ? 
      margin_left : margin_left + (gfx_w / 2);
    gfx_y = top = margin_top + (1+( i % split_at)) * (gfx_texth +5);
    sprintf(#head, "%02d. %s", i+1, art_keys_str + i);
    gfx_drawstr(#head);
    gfx_x = note_width+left;
    gfx_drawstr(art_match_strings + i);
    i == ACTIVE_ART ? (
      gfx_r = gfx_b = 0;
      gfx_g = 1;
      gfx_a = 0.4;
      gfx_rect(left-margin_left, top, gfx_x, gfx_texth);
      gfx_r = gfx_b = gfx_g = text_colour;
      gfx_a = 1;
    );
    i += 1;
  );
  drawn_art = ACTIVE_ART;
);
